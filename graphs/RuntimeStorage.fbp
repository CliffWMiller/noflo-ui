INPORT=LoadData.DB:DB
INPORT=Dispatch.IN:IN
INPORT=FindRuntimes.CONTEXT:CONTEXT
OUTPORT=MergeContext.OUT:CONTEXT
OUTPORT=UpdateLibrary.RUNTIME:NEW

# Route requests
'runtime:open,runtime:connect,runtime:sendGraph,runtime:sendComponent,runtime:runTests' -> ROUTES Dispatch(ui/DispatchAction)
Dispatch HANDLING -> START Loading(ui/CreateLoadingContext)
Loading OUT -> IN MergeContext(core/Merge)

# Build direct runtime context
Dispatch HANDLE[0] -> IN CleanDirectContext(ui/CleanAction) OUT -> CONTEXT DirectRuntime(ui/DirectRuntime)
DirectRuntime CONTEXT -> IN MergeContextPreSubscribe(core/Merge)

# Add runtime connection to an existing context
Dispatch HANDLE[1] -> IN CleanProjectContext(ui/CleanAction) OUT -> CONTEXT ProjectRuntime(ui/ProjectRuntime)

# Add compatible runtimes to already-populated contexts
FindRuntimes(ui/FindRuntimes) CONTEXT -> CONTEXT ProjectRuntime(ui/ProjectRuntime)
ProjectRuntime CONTEXT -> IN MergeContextPreSubscribe(core/Merge)

# Get components list when connecting to new runtime
ProjectRuntime RUNTIME -> RUNTIME UpdateLibrary(ui/UpdateComponentLibrary)
DirectRuntime RUNTIME -> RUNTIME UpdateLibrary
ProjectRuntime RUNTIME -> RUNTIME ListComponents(runtime/ListComponents)
DirectRuntime RUNTIME -> RUNTIME ListComponents
ListComponents OUT -> IN UpdateLibrary
UpdateLibrary OUT -> IN MergeContext
ListComponents ERROR -> IN ShowErrors

# Graph change subscription
MergeContextPreSubscribe OUT -> CONTEXT SubscribeGraph(ui/SubscribeGraph)
SubscribeGraph RUNTIME -> RUNTIME SendGraph(runtime/SendGraphChanges)
SubscribeGraph GRAPH -> GRAPH SendGraph
SendGraph ERROR -> IN ShowErrors
SubscribeGraph OUT -> IN MergeContext(core/Merge)

SubscribeGraph RUNTIME -> RUNTIME ListenNetwork(runtime/ListenNetwork)
SubscribeGraph GRAPH -> GRAPH ListenNetwork
ListenNetwork PACKET -> PACKET PacketToContext(ui/PacketToContext)
PacketToContext OUT -> IN MergeContext
ListenNetwork PROCESSERROR -> ERROR ProcessErrorToContext(ui/ProcessErrorToContext)
ProcessErrorToContext OUT -> IN MergeContext

# Sending newly-created graphs and components to runtime
Dispatch HANDLE[2] -> IN CleanNewGraph(ui/CleanAction) OUT -> GRAPH SendNewGraph(runtime/SendGraph)
ProjectRuntime CONNECTED -> RUNTIME SendNewGraph
DirectRuntime RUNTIME -> RUNTIME SendNewGraph
Dispatch HANDLE[3] -> IN CleanNewComponent(ui/CleanAction) OUT -> COMPONENT SendNewComponent(runtime/SendComponent)
ProjectRuntime CONNECTED -> RUNTIME SendNewComponent
DirectRuntime RUNTIME -> RUNTIME SendNewComponent

# Running fbp-spec tests on the runtime
Dispatch HANDLE[4] -> IN CleanTests(ui/CleanAction) OUT -> IN RunTests(ui/RunTests)
ProjectRuntime CONNECTED -> RUNTIME RunTests
DirectRuntime RUNTIME -> RUNTIME RunTests
RunTests CONTEXT -> IN MergeContext
RunTests ERROR -> IN ShowErrors

LoadData ERROR -> IN ShowErrors
