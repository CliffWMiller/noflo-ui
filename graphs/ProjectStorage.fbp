INPORT=SplitDb.IN:DB
INPORT=Dispatch.IN:IN
OUTPORT=MergeContext.OUT:CONTEXT

# Route opening requests
'open,save,delete' -> ROUTES Dispatch(routers/GroupRouter)
Dispatch ROUTE -> START Loading(ui/CreateLoadingContext)
Loading OUT -> IN MergeContext(core/Merge)
Dispatch MISSED -> IN ShowErrors(core/Output)

# Pass database connection to UI for Journal usage
SplitDb OUT -> START CreateDbCtx(objects/CreateObject) OUT -> CONTEXT SetDb(ui/SetToContext)
'db' -> KEY SetDb
SplitDb OUT -> VALUE SetDb CONTEXT -> IN MergeContext

# Pass data to project context builder
Dispatch OUT[0] -> IN PopulateProjectData(ui/PopulateProjectData)
PopulateProjectData OUT -> IN MergeContext

# Save handling for current graph
PopulateProjectData CURRENTGRAPH -> IN ListenGraph(graph/ListenChanges)
'graph' -> GROUP SetGraphGroup(groups/Group)
ListenGraph OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN SetGraphGroup OUT -> IN SetChanged(ui/SetGraphChanged) OUT -> IN Save

# Persistence
SplitDb OUT -> DB Save(ui/Save)
Dispatch OUT[1] -> IN Save
Save OUT -> IN MergeContext

SplitDb OUT -> DB Delete(ui/Delete)
Dispatch OUT[2] -> IN Delete
Save OUT -> IN MergeContext
